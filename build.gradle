plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "io.github.pacifistmc.forgix" version "1.2.6"
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

forgix {
    group = rootProject.maven_group
    mergedJarName = "${rootProject.archives_base_name}-${rootProject.mod_version}-all+${rootProject.minecraft_version}.jar"
    outputDir = "build/libs/Merged"

    forge {
        projectName = "forge"
        jarLocation = "build/libs/${rootProject.archives_base_name}-${rootProject.mod_version}.jar"

        mixin "walljump-common.mixins.json"
    }

    fabric {
        projectName = "fabric"
        jarLocation = "build/libs/${rootProject.archives_base_name}-${rootProject.mod_version}.jar"
    }

    quilt {
        projectName = "quilt"
        jarLocation = "build/libs/${rootProject.archives_base_name}-${rootProject.mod_version}.jar"
    }
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.officialMojangMappings()
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:1.19+build.4:v2"
    }
    
    build.finalizedBy(mergeJars)
    assemble.finalizedBy(mergeJars)
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven { url "https://maven.terraformersmc.com" }
        maven { url "https://maven.shedaniel.me/" }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
